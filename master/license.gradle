license {
	encoding= "utf8"
	header = file("../master/lgpl.template")
	ext.year = project.inceptionYear
	ext.name = project.developerName
	ext.email = project.developerEmail
	ext.program= project.baseName
	includes(["**/*.java", "**/*.groovy"])
	excludes(["src/test/**/*.txt", "**/README.txt"])
	strictCheck=true
	useDefaultMappings=true
	dryRun=false
	mapping{
		txt="TEXT"
	}
}

task copyGradleLicense(type: Copy) {
	from file("../master/license/gradle.license")
	into './license'
}

task copyLicense(type: Copy) {
	from file("../master/license/COPYING")
	from file("../master/license/COPYING.LESSER")
	into './'
}


task copyLicenseText(type: Copy) {
	from file("../master/lgpl.template")
	into './'
	rename {filename->"LICENSE.txt"}
	expand([
		year: project.inceptionYear,
		name: project.developerName,
		email: project.developerEmail,
		program: project.baseName
	])
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment {
				MavenDeployment deployment -> signing.signPom(deployment)
			}

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}
			
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}
			pom {
				project {
					inceptionYear project.inceptionYear
					licenses {
						license {
							name 'The GNU Lesser General Public License, version 3.0 (LGPL-3.0)'
							url 'https://opensource.org/licenses/LGPL-3.0'
							distribution 'repo'
						}
					}
					scm {
						url project.scmUrl
						connection project.scmConnection
						developerConnection project.scmDeveloperConnection
					}
					developers {
						developer {
							id project.sonatypeUsername
							name project.sonatypeFullname
							email project.developerEmail
							url project.developerUrl
						}
					}
					name (project.group+':'+project.baseName)
					url project.url
					description project.description
				}
			}
		}
	}
}

licenseFormat.dependsOn copyGradleLicense,copyLicense,copyLicenseText
compileJava.dependsOn licenseFormat
